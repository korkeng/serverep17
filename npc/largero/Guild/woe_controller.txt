// Information NPC
//============================================================
-	script	WOE Information	835,{
	doevent "WOE_CONTROL::OnMenu";
	end;
	
	OnInit:
		//set .waitingroom,1;		// แสดงห้องบนหัว NPC
		setarray .castle_map$, "prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
							   "payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
							   "gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
							   "aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05";
		sleep 1000;
		while(.waitingroom){
			for(set .@i, 0; .@i < getarraysize(.castle_map$); set .@i, .@i + 1){
				set .@sum, .@sum + getmapusers(.castle_map$);
			}
			//waitingroom "# GvG [ " + .@sum + " ] คน",0;
			set .@sum, 0;
			sleep 1500;
			delwaitingroom;
		}
		
while (1) {
			showscript "[ GvG Castle ]";
			sleep 500;
		}
end;
}

// Script Core
//============================================================
-	script	WOE_CONTROL	-1,{
function Disp_Owner; function Add_Zero;
function Region;
function Castle;

OnInit:
// -----------------------------------------------------------
//  Configuration settings.
// -----------------------------------------------------------
	// ================== ตั้งค่าทั่วไป ==================
	set .CastleWarp,0;		// 1: เปิดวาร์ปทุกปราสาท | 0: เปิดวาร์ปเฉพาะบ้านที่วอร์อยู่เท่านั้น
	set .AutoKick,1;		// เตะผู้เล่นออกจากบ้านที่ไม่ได้ทำการเปิดวอร์ไว้ (1: เตะ / 0: ไม่เตะ)
	set .NoOwner,0; 		// เตะผู้เล่นออกจากบ้านที่ไม่มีผู้ใดยึดครอง (1: เตะ / 0: ไม่เตะ)
	set .ExitWarp,0;		// ส่งผู้เล่นกลับเมืองเมื่อสงครามกิลด์วอร์สิ้นสุดลง (1: ส่ง / 0: ไม่ส่ง)
	set .GMAccess,99;		// สิทธิ์ในการเข้าถึงการตั้งค่าของ NPC (ควรเป็น 99)
	
	// ================== ตั้งค่ารางวัลบ้านวอร์ ==================
	// วิธีตั้งค่า สมมุติต้องการว่าอยากได้ 1 กับ 8  - >( 1|8 )
	set .Options, 1|4;					// เลือกระบบที่ต้องการแจก  1 = เปิดการแจกรางวัล | 
										// 2 = แจกโดยการส่งเข้า mail (rodEx) | 
										// 4 = แจกเฉพาะหัวหน้ากิลด์ที่วอร์ได้ปราสาท | 
										// 8 = ตรวจสอบ IP internet หากมาจาก ip เดียวกันจะไม่ได้รางวัลซ้ำ
	setarray .reward_id[0],61004,61005,61080,61006;		// รหัสไอเทมที่จะแจก
	setarray .reward_amount[0],30,300,30,3000;		// จำนวนไอเทมที่แจก ( เรียงตามรหัสไอเทมด้านบน )
	set .reward_zeny, 500000000;				// จำนวนเงิน Zeny ที่จะแจก
	
	// ================== ไม่ต้องแก้ไข ==================
	
						   

// -----------------------------------------------------------
//  Castles datas.
// -----------------------------------------------------------

// Region function defines the region of the castle defined by Castle function.
// Region("<region name>");
// Castle("<castle map name>", "<event name when woe end>", "<event killmonster name>", "<map name special warp>",<x>,<y>)

	Region("Prontera");
		Castle("prtg_cas01", "Agit#prtg_cas01::OnAgitEnd", "Agit#prtg_cas01::OnAgitBreak", "prt_gld",134,65);
		Castle("prtg_cas02", "Agit#prtg_cas02::OnAgitEnd", "Agit#prtg_cas02::OnAgitBreak", "prt_gld",240,128);
		Castle("prtg_cas03", "Agit#prtg_cas03::OnAgitEnd", "Agit#prtg_cas03::OnAgitBreak", "prt_gld",153,137);
		Castle("prtg_cas04", "Agit#prtg_cas04::OnAgitEnd", "Agit#prtg_cas04::OnAgitBreak", "prt_gld",111,240);
		Castle("prtg_cas05", "Agit#prtg_cas05::OnAgitEnd", "Agit#prtg_cas05::OnAgitBreak", "prt_gld",208,240);
	Region("Payon");
		Castle("payg_cas01", "Agit#payg_cas01::OnAgitEnd", "Agit#payg_cas01::OnAgitBreak", "pay_gld",121,233);
		Castle("payg_cas02", "Agit#payg_cas02::OnAgitEnd", "Agit#payg_cas02::OnAgitBreak", "pay_gld",295,116);
		Castle("payg_cas03", "Agit#payg_cas03::OnAgitEnd", "Agit#payg_cas03::OnAgitBreak", "pay_gld",317,293);
		Castle("payg_cas04", "Agit#payg_cas04::OnAgitEnd", "Agit#payg_cas04::OnAgitBreak", "pay_gld",140,160);
		Castle("payg_cas05", "Agit#payg_cas05::OnAgitEnd", "Agit#payg_cas05::OnAgitBreak", "pay_gld",204,266);
	Region("Geffen");
		Castle("gefg_cas01", "Agit#gefg_cas01::OnAgitEnd", "Agit#gefg_cas01::OnAgitBreak", "gef_fild13",214,75);
		Castle("gefg_cas02", "Agit#gefg_cas02::OnAgitEnd", "Agit#gefg_cas02::OnAgitBreak", "gef_fild13",308,240);
		Castle("gefg_cas03", "Agit#gefg_cas03::OnAgitEnd", "Agit#gefg_cas03::OnAgitBreak", "gef_fild13",143,240);
		Castle("gefg_cas04", "Agit#gefg_cas04::OnAgitEnd", "Agit#gefg_cas04::OnAgitBreak", "gef_fild13",193,278);
		Castle("gefg_cas05", "Agit#gefg_cas05::OnAgitEnd", "Agit#gefg_cas05::OnAgitBreak", "gef_fild13",305,87);
	Region("Aldebaran");
		Castle("aldeg_cas01", "Agit#aldeg_cas01::OnAgitEnd", "Agit#aldeg_cas01::OnAgitBreak", "alde_gld",48,83);
		Castle("aldeg_cas02", "Agit#aldeg_cas02::OnAgitEnd", "Agit#aldeg_cas02::OnAgitBreak", "alde_gld",95,249);
		Castle("aldeg_cas03", "Agit#aldeg_cas03::OnAgitEnd", "Agit#aldeg_cas03::OnAgitBreak", "alde_gld",142,85);
		Castle("aldeg_cas04", "Agit#aldeg_cas04::OnAgitEnd", "Agit#aldeg_cas04::OnAgitBreak", "alde_gld",239,242);
		Castle("aldeg_cas05", "Agit#aldeg_cas05::OnAgitEnd", "Agit#aldeg_cas05::OnAgitBreak", "alde_gld",264,90);

	setarray .Days$[0],"วันอาทิตย์","วันจันทร์","วันอังคาร","วันพุธ","วันพฤหัสบดี","วันศุกร์","วันเสาร์";

// -----------------------------------------------------------

	.reward_id_size = getarraysize(.reward_id);
	set .Size, getarraysize($WOE_CONTROL);

	if (.AutoKick || .NoOwner) {
		for(set .@i,0; .@i<.total_castle; set .@i,.@i+1) {
			setmapflag .Castles$[.@i], mf_loadevent;
			setd "."+.Castles$[.@i], .@i;
		}
	}
	else {
		for(set .@i,0; .@i<.total_castle; set .@i,.@i+1)
			setd "."+.Castles$[.@i], 0;
	}
	if (!agitcheck() && !agitcheck2() && !agitcheck3()) sleep 4000;
	set .Init,1;
	.current_region = 0;

OnMinute00:
	freeloop(1);
	if (agitcheck() || agitcheck2() || agitcheck3()) {
		for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
			if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) == $WOE_CONTROL[.@i+2]) {
			OnWOEEnd:
				announce "สงครามกิลด์วอร์ได้สิ้นสุดลงแล้ว !",bc_all|bc_woe;
				AgitEnd; AgitEnd2; AgitEnd3;
				sleep 1000;
				for(set .@j,0; .@j<.total_castle; set .@j,.@j+1) {
					if (.Active[0]&(1<<.@j)) Disp_Owner(.Castles$[.@j],1);
					if (.ExitWarp) maprespawnguildid .Castles$[.@j],0,3;
				}
				if ((.Options&1) && .Active[0] && .ForceEnd != 2) callsub OnReward, .Active[0];
				deletearray .Active[0],2;
				if (.ForceEnd) { set .ForceEnd,0; end; }
				break;
			}
		}
	}
	.@agit_check = (!agitcheck() && !agitcheck2() && !agitcheck3());
	if (.@agit_check || .Init) {
		if (.@agit_check) set .Init,0;
		for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
			if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) >= $WOE_CONTROL[.@i+1] && gettime(DT_HOUR) < $WOE_CONTROL[.@i+2]) {
				deletearray .Active[0],2;
				set .Active[0], $WOE_CONTROL[.@i+3];
				if (.Init) { AgitEnd; AgitEnd2; AgitEnd3; }
				else announce " สงครามกิลด์วอร์ได้เริ่มต้นขึ้นแล้ว !",bc_all|bc_woe;
				sleep 1000;
				AgitStart; AgitStart2; AgitStart3;
				for(set .@j,0; .@j<.total_castle; set .@j,.@j+1) {
					if (.Active[0]&(1<<.@j)) {
						if (!.Init) Disp_Owner(.Castles$[.@j],0);
						set .Active[1], .Active[1] | (1<<.castle_region[.@j]);
					} else {
						donpcevent .event_name_agitend$[.@j];
						killmonster .Castles$[.@j], .event_name_killmob$[.@j];
					}
				}
				break;
			}
		}
	}
	set .Init,0;
	end;

function Disp_Owner {
	set .@o, getcastledata(getarg(0),1);
	if (.@o) announce "The ["+getcastlename(getarg(0))+"] castle "+((getarg(1))?"has been conquered":"is currently held")+" by the ["+getguildname(.@o)+"] guild.",bc_all|bc_woe;
	else announce "The ["+getcastlename(getarg(0))+"] castle is currently unoccupied.",bc_all|bc_woe;
	return;
}
function Add_Zero {
	return ((getarg(0)<10)?"0":"")+getarg(0)+(getarg(1,0)?".":":")+"00";
}
function Castle {
	.@map$ = getarg(0);
	if (getd( "." + .@map$ ) > 0) {
		debugmes sprintf("WOE_CONTROL: map %s already defined.", .@map$);
		return;
	}
	if (.total_castle > 61) {
		debugmes sprintf("WOE_CONTROL: Maximum of 62 castles defined, %s skipped.", .@map$);
		return;
	}
	.Castles$[.total_castle] = .@map$;
	.event_name_agitend$[.total_castle] = getarg(1);
	.event_name_killmob$[.total_castle] = getarg(2);
	.Map$[.total_castle] = getarg(3);
	.MapX[.total_castle] = getarg(4);
	.MapY[.total_castle] = getarg(5);
	.castle_region[.total_castle] = .current_region;

	setd ".castle_index_in_region_" + .current_region + "[" + .size_region[.current_region] + "]", .total_castle;
	.total_castle++;
	.size_region[.current_region]++;

	setd "." + .Castles$[.total_castle-1], .total_castle;
	return;
}
function Region {
	.@zone$ = getarg(0);
	for ( .@i = 0; .@i < .total_region; ++.@i ) {
		if (.Regions$[.@i] == .@zone$) {
			.current_region = .@index;
			return;
		}
	}
	.Regions$[.total_region] = .@zone$;
	.current_region = .total_region;
	.total_region++;
	return;
}

OnReward:
	if (!.reward_id_size && !.reward_zeny)
		return;

	if (.Options&2) set .@str$,gettimestr("%B %d, %Y",21);
	freeloop(1);
	for(set .@i,0; .@i<.total_castle; set .@i,.@i+1) {
		if (getarg(0)&(1<<.@i)) {
			set .@gid, getcastledata(.Castles$[.@i],1);
			if (!.@gid)
				continue;

			getguildmember( .@gid, 1, .@cid );
			.@size_guild = getguildmember( .@gid, 2, .@aid );

			if( .Options&4 ){
				.@master_cid = getguildmasterid( .@gid );
				.@index = inarray(.@cid, .@master_cid);
				.@master_aid = .@aid[.@index];

				cleararray( .@cid, 0, .@size_guild );
				cleararray( .@aid, 0, .@size_guild );

				.@size_guild = 1;
				.@cid[0] = .@master_cid;
				.@aid[0] = .@master_aid;
			}

			for(set .@j,0; .@j<.@size_guild; set .@j,.@j+1) {
				if ((.Options&8) && !(.Options&4)) {
					set .@ip$, replacestr(getcharip(.@aid[.@j]),".","a");
					if (getd(".@ip_"+.@i+"_"+.@ip$)) continue;
					setd ".@ip_"+.@i+"_"+.@ip$,1;
				}
				if (.Options&2) {
					.@charid = .@cid[.@j];
					.@sender$ = "no-reply";
					.@title$ = "** Siege Reward: "+getcastlename(.Castles$[.@i])+" **";
					.@body$ = "Brave one,\r\n \r\n Congratulations!\r\n Your guild has successfully occupied\r\n territory in the War of Emperium on\r\n "+.@str$+".\r\n \r\n \r\n \r\n \r\n [ Your reward is attached. ]";

					if (.reward_id_size)
						mail .@charid, .@sender$, .@title$, .@body$, .reward_zeny, .reward_id, .reward_amount;
					else
						mail .@charid, .@sender$, .@title$, .@body$, .reward_zeny;

					if (PACKETVER < 20150513 && !getd(".@str_"+.@cid[.@j]) && isloggedin(.@aid[.@j],.@cid[.@j])) {
						setd ".@str_"+.@cid[.@j],1;
						message rid2name(.@aid[.@j]),"You've got mail!";
					}
				}
				else if (isloggedin(.@aid[.@j],.@cid[.@j])) {
					attachrid( .@aid[.@j], true );
					.@name$ = strcharinfo( 0 );
					.@castle_name$ = getcastlename(.Castles$[.@i]);
					for ( .@k = 0; .@k < .reward_id_size; .@k++ ) {
						if (checkweight(.reward_id[.@k], .reward_amount[.@k]))
							getitem .reward_id[.@k], .reward_amount[.@k];
						else
							dispbottom "You can't receive x" + .reward_amount[.@k] + " " + getitemname(.reward_id[.@k]) + " for conquering " + .@castle_name$ + " because you're overweight.";
					}
					Zeny += .reward_zeny;
					dispbottom "You have been rewarded for conquering " + .@castle_name$ + ".";
					detachrid;
				}
			}
		}
	}
	return;

OnPCLoadMapEvent:
	.@compare_val = compare(strcharinfo(3),"g_cas");
	if (!.@compare_val) end;
	if (((.AutoKick && .Active[0]) || (.NoOwner && !getcastledata(strcharinfo(3),1))) && !(.Active[0]&(1<<getd("."+strcharinfo(3))))) {
		if (getcharid(2) && getcastledata(strcharinfo(3),1) == getcharid(2)) end;
		.@castle_name$ = getcastlename(strcharinfo(3));
		sleep2 1000;
		message strcharinfo(0), .@castle_name$ + " is currently inactive.";
		sleep2 5000;
		if (.@compare_val) warp "SavePoint",0,0;
	}
	end;

OnMenu:
while(1) {
	mes "[WOE Information]";
	if (agitcheck() || agitcheck2() || agitcheck3()) {
		if (.Active[0]) {
			for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
				if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) >= $WOE_CONTROL[.@i+1] && gettime(DT_HOUR) < $WOE_CONTROL[.@i+2]) {
					set .@i, $WOE_CONTROL[.@i+2];
					break;
				}
			}
			mes "สงครามจะสิ้นสุดลงเมื่อ ^FF0000"+Add_Zero(.@i)+"^000000 สำหรับปราสาท :";
			mes " ";
			for(set .@i,0; .@i < .total_region; set .@i,.@i+1)
				if (.Active[1]&(1<<.@i)) mes " > ^777777"+.Regions$[.@i]+"^000000";
		} else
			mes "ขณะนี้อยู่ในช่วงเวลา ^0055FFสงครามกิลด์วอร์^000000.";
	} else {
		for(set .@i,0; .@i<.Size; set .@i,.@i+4)
			if ((gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) <= $WOE_CONTROL[.@i+1]) || gettime(DT_DAYOFWEEK) < $WOE_CONTROL[.@i]) {
				setarray .@time[0],$WOE_CONTROL[.@i],$WOE_CONTROL[.@i+1];
				break;
			}
		if (!getarraysize(.@time))
			setarray .@time[0],$WOE_CONTROL[0],$WOE_CONTROL[1];
		mes "^777777ไม่มีสงคราม^000000กิลด์วอร์ ณ เวลานี้";
		if (.Size) {
			mes " ";
			mes "สงครามครั้งถัดไปจะเริ่มในวันที่";
			mes "^0055FF"+.Days$[.@time[0]]+"^000000 เวลา "+Add_Zero(.@time[1])+"^000000 น.";
		}
	}
	next;
	switch(select(""+((.CastleWarp || .Active[1])?"  วาร์ปไปยังปราสาท...":"")+":  ตรวจสอบเวลาวอร์...:  ตรวจสอบผู้ครอบครองปราสาท...:"+((getgmlevel()<.GMAccess || !getgmlevel())?"":"  เพิ่ม - ลด เวลากิลด์วอร์...:") + ((getgmlevel()<.GMAccess || !getgmlevel())?"":"  เปิด - ปิด ระบบวาร์ปชั้น 2...:") + "  ^777777ยกเลิก^000000")) {
	case 1:
		if (.CastleWarp) set .@clr$,"^0055FF";
		set .@menu$,"";
		for(set .@i,0; .@i < .total_region; set .@i,.@i+1) {
			if (.CastleWarp || .Active[1]&(1<<.@i))
				set .@menu$, .@menu$+"  "+((.Active[1]&(1<<.@i))?.@clr$:"^777777")+.Regions$[.@i]+" Castles^000000";
			set .@menu$, .@menu$+":";
		}
		set .@i, select(.@menu$)-1;
		set .@menu$,"";
		for(set .@j,0; .@j < .size_region[.@i]; set .@j,.@j+1) {
			.@index = getd( ".castle_index_in_region_" + .@i + "[" + .@j + "]" );
			if (.CastleWarp || .Active[0]&(1<<.@index))
				set .@menu$, .@menu$+"  "+((.Active[0]&(1<<.@index))?.@clr$:"^777777")+getcastlename(.Castles$[.@index])+"^000000";
			set .@menu$, .@menu$+":";
		}
		set .@j, select(.@menu$)-1;
		.@index = getd( ".castle_index_in_region_" + .@i + "[" + .@j + "]" );
		warp .Map$[.@index],.MapX[.@index],.MapY[.@index];
		close;
	case 2:
		mes "[Schedule]";
		if (.Size) {
			freeloop(1);
			for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
				mes "> ^FF0000"+.Days$[$WOE_CONTROL[.@i]]+" ("+Add_Zero($WOE_CONTROL[.@i+1])+"-"+Add_Zero($WOE_CONTROL[.@i+2])+")^000000";
				for(set .@j,0; .@j<.total_castle; set .@j,.@j+1)
					if ($WOE_CONTROL[.@i+3]&(1<<.@j)) mes "   "+getcastlename(.Castles$[.@j])+" ^777777("+.Castles$[.@j]+")^000000";
				if (.@i+4 < .Size) mes " ";
			}
			freeloop(0);
		} else
			mes "ไม่มีการตั้งค่าสงคราม ณ ขณะนี้";
		next;
		break;
	case 3:
		mes "[Castle Ownership]";
		for(set .@i,0; .@i < .total_region; set .@i,.@i+1) {
			set .@k, .@i*5;
			mes "> ^FF0000"+.Regions$[.@i]+"^000000";
			for(set .@j,0; .@j<.size_region[.@i]; set .@j,.@j+1) {
				.@index = getd( ".castle_index_in_region_" + .@i + "[" + .@j + "]" );
				set .@t, getcastledata(.Castles$[.@index],1);
				mes "   "+getcastlename(.Castles$[.@index])+": "+((.@t)?"^0055FF"+getguildname(.@t):"^777777<ไม่มี>")+"^000000";
			}
			if (.@i < 5) mes " ";
		}
		next;
		break;
	case 4:
	while(1) {
		mes "[Session Manager]";
		mes "กิลด์วอร์ที่ถูกตั้งค่า ^0055FF"+(.Size/4)+"^000000 เวลา";
		mes " ";
		mes "ท่านต้องการจะทำอะไรหล่ะ ?";
		next;
		switch(select("  เพิ่มกิลด์วอร์..:  ลดกิลด์วอร์...:  รีโหลดการตั้งค่า...:"+((agitcheck() || agitcheck2() || agitcheck3())?"  บังคับจบกิลด์วอร์...":"")+":  ^777777ออกจากหน้านี้^000000")) {
		case 1:
			mes "[New Session]";
			mes "เลือกวันที่ท่านต้องการเปิดกิลด์วอร์";
			next;
			set .@Day, select("  "+implode(.Days$,":  "))-1;
			mes "[New Session]";
			mes "กำหนดเวลาการเริ่มสงคราม ^0055FF"+.Days$[.@Day]+"^000000";
			next;
			set .@menu$,"";
			for(set .@i,0; .@i<23; set .@i,.@i+1)
				set .@menu$, .@menu$+"  "+Add_Zero(.@i,1)+":";
			set .@Start, select(.@menu$)-1;
			mes "[New Session]";
			mes "กำหนดเวลาในการสิ้นสุดสงคราม ^0055FF"+.Days$[.@Day]+"^000000";
			next;
			set .@menu$,"";
			for(set .@i,.@Start+1; .@i<24; set.@i,.@i+1)
				set .@menu$, .@menu$+"  "+Add_Zero(.@i,1)+":";
			set .@End, select(.@menu$)+.@Start;
			for(set .@i,0; .@i<.Size; set .@i,.@i+4)
				if (.@Day == $WOE_CONTROL[.@i] &&
				   ((.@Start >= $WOE_CONTROL[.@i+1] && .@Start < $WOE_CONTROL[.@i+2]) ||
				    (.@End > $WOE_CONTROL[.@i+1] && .@End <= $WOE_CONTROL[.@i+2]) ||
				    (.@Start <= $WOE_CONTROL[.@i+1] && .@End >= $WOE_CONTROL[.@i+2]))) {
					mes "[New Session]";
					mes "ขออภัย เวลาที่ท่านเลือกซ้ำแล้ว";
					next;
					set .@d,1;
					break;
				}
			if (.@d) { set .@d,0; break; }
			set .@Castle,0;
			while(1) {
				mes "[New Session]";
				mes "^0055FF"+.Days$[.@Day]+" ("+Add_Zero(.@Start)+"-"+Add_Zero(.@End)+")^000000";
				mes " > Castles:";
				if (!.@Castle)
					mes "    ^777777(ยังไม่ได้เลือก)^000000";
				else {
					for(set .@i,0; .@i<.total_castle; set .@i,.@i+1) {
						.@string$ = ( .@Castle&(1<<.@i) ) ? "    "+getcastlename(.Castles$[.@i])+" ("+.Castles$[.@i]+")" : "";
						mes .@string$;
					}
				}
				next;
				set .@menu$,((.@Castle)?"  ^FF0000Finished...^000000":"")+":";
				for(set .@i,0; .@i<.total_castle; set .@i,.@i+1)
					set .@menu$, .@menu$+"  "+((.@Castle&(1<<.@i))?"^0055FF":"")+getcastlename(.Castles$[.@i])+" ("+.Castles$[.@i]+")^000000:";
				set .@i, select(.@menu$)-1;
				if (.@i)
					set .@Castle, .@Castle^(1<<(.@i-1));
				else {
					mes "[New Session]";
					mes "ท่านมั่นใจหรือไม่";
					next;
					switch(select("  ^0055FFมั่นใจแล้วเอาแค่นี้...^000000:  เพิ่มปราสาทในช่วงเวลานี้อีก...:  ^777777ยกเลิก^000000")) {
					case 1:
						for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
							if ((.@Day == $WOE_CONTROL[.@i] && .@End <= $WOE_CONTROL[.@i+1]) || .@Day < $WOE_CONTROL[.@i]) {
								set .@d,1;
								break;
							}
						}
						if (!.@d) { set .@d,1; set .@i,.Size; }
						copyarray $WOE_CONTROL[.@i+4], $WOE_CONTROL[.@i], .Size-.@i;
						setarray $WOE_CONTROL[.@i], .@Day, .@Start, .@End, .@Castle;
						set .Size, getarraysize($WOE_CONTROL);
					case 3:
						mes "[New Session]";
						mes ((.@d)?"เพิ่มเวลาเรียบร้อยแล้ว":"ยกเลิกเรียบร้อยแล้ว");
						next;
						set .@d,1;
					case 2:
						break;
					}
					if (.@d) { set .@d,0; break; }
				}
			}
			break;
		case 2:
			mes "[Remove Session]";
			if (!.Size) {
				mes "ยังไม่มีการตั้งค่ากิลด์วอร์ใดๆ";
				next;
				break;
			}
			mes "เลือกเมนูที่ท่านต้องการจะลบ";
			next;
			set .@menu$,"";
			for(set .@i,0; .@i<.Size; set .@i,.@i+4)
				set .@menu$, .@menu$+"  "+.Days$[$WOE_CONTROL[.@i]]+" ("+Add_Zero($WOE_CONTROL[.@i+1],1)+"-"+Add_Zero($WOE_CONTROL[.@i+2],1)+"):";
			set .@menu$, .@menu$+"  ^777777Cancel^000000";
			set .@i, select(.@menu$)-1;
			if (.@i == (.Size/4)) break;
			mes "[Remove Session]";
			mes "ลบ ^0055FF"+.Days$[$WOE_CONTROL[.@i*4]]+"^000000 ใช่ไหม ?";
			mes "หากลบแล้วจะนำกลับมาไม่ได้นะ";
			next;
			set .@j, select("  ^FF0000แน่ใจแล้ว ลบเลย...^000000:  ^777777เก็บไว้ก่อนดีกว่า^000000");
			mes "[Remove Session]";
			if (.@j == 2)
				mes "ยกเลิกเรียบร้อยแล้ว.";
			else {
				deletearray $WOE_CONTROL[.@i*4],4;
				set .Size, getarraysize($WOE_CONTROL);
				mes "Session deleted.";
			}
			next;
			break;
		case 3:
			mes "[Reload Settings]";
			mes "การรีโหลดจะมีผลกระทบต่อทุกๆ การตั้งค่า หรือสคริปที่เกี่ยวข้อง";
			if (agitcheck() || agitcheck2() || agitcheck3()) {
				mes " ";
				mes "ตรวจพบว่ายังมีสงครมอยู่ ณ ขณะนี้ท่านแน่ใจว่าจะรีโหลดใช่ไหม";
			}
			next;
			set .@i, select("  ^0055FFรีโหลด เดี๋ยวนี้...^000000:  ^777777ยกเลิก^000000");
			mes "[Reload Settings]";
			if (.@i == 2) mes "ยกเลิกแล้ว";
			else {
				set .Init,1;
				donpcevent "WOE_CONTROL::OnMinute00";
				mes "การรีโหลดสำเร็จแล้ว";
			}
			next;
			break;
		case 4:
			mes "[Force Agit End]";
			if (!agitcheck() && !agitcheck2() && !agitcheck3())  {
				mes "ขณะนี้ไม่มีกิลด์วอร์ดำเนินอยู่";
				next;
				break;
			}
			mes "การสั่งบังคับยกเลิกกิลด์วอร์นี้เป็นระบบที่ปลอดภัย";
			mes " ";
			mes "ท่านแน่ใจหรือไม่ที่จะสิ้นสุด";
			mes "กิลด์วอร์ครั้งนี้";
			next;
			set .@i, select("  ^FF0000สิ้นสุด โดยแจกรางวัล ...^000000:"+((.Options&1)?"  ^FF0000สิ้นสุด โดยไม่ให้รางวัล...^000000":"")+":  ^777777ยกเลิก^000000");
			mes "[Force Agit End]";
			if (.@i == 3)
				mes "ยกเลิกแล้ว";
			else {
				set .ForceEnd, .@i;
				donpcevent "WOE_CONTROL::OnWOEEnd";
				mes "สงครามได้สิ้นสุดลงแล้ว";
			}
			next;
			break;
		case 5:
			set .@d,1; break;
		}
		if (.@d) { set .@d,0; break; }
		} break;
	case 5:
		mes "[Warp Enable]";
		mes "ขณะนี้ระบบเปิดวาร์ปชั้นที่ 2";
		mes "ถูก " + ($SAV_ONLINE ? "^239A0Eเปิด^000000" : "^E83131ปิด^000000") + " อยู่ ท่านต้องการ";
		mes "จะดำเนินการ " + ($SAV_ONLINE ? "ปิด" : "เปิด") + " หรือไม่ ?";
		next;
		if(select(($SAV_ONLINE ? " ดำเนินการปิด" : " ดำเนินการเปิด")," ยกเลิก") == 2 ) close;
		mes "[Warp Enable]";
		mes "ทำการ " + ($SAV_ONLINE ? "ปิด" : "เปิด")  + " ระบบวาร์ป";
		mes "เรียบร้อยแล้ว !!";
		set $SAV_ONLINE, ($SAV_ONLINE ? 0 : 1);
		close;
	default:
		close;
	} }
}

-	script	empWarp	-1,{

	OnAgitStart:
		disablenpc "prtg-1-06_prtg-1-11"; disablenpc "prtg-2-05_prtg-2-11"; disablenpc "prtg-2-04_prtg-2-12"; disablenpc "prtg-3-05_prtg-3-13"; disablenpc "prtg-4-02_prtg-4-14"; disablenpc "prtg-4-06_prtg-4-13"; disablenpc "prtg-5-01_prtg-5-14";
		disablenpc "gefg-1-01_gefg-1-14"; disablenpc "gefg-2-01_gefg-2-12"; disablenpc "g3-cas3_g3-in3"; disablenpc "gefg-4-01_gefg-4-11"; disablenpc "gefg-5-06_gefg-5-11";
		disablenpc "payg-1-02_payg-1-11"; disablenpc "payg-2-05_payg-2-11"; disablenpc "payg-3-04_payg-3-13"; disablenpc "payg-4-03_payg-4-12"; disablenpc "payg-5-02_payg-5-14";
		disablenpc "aldeg-1-4_aldeg-1-10"; disablenpc "aldeg-2-5_aldeg-2-6"; disablenpc "aldeg-3-14_aldeg-3-5"; disablenpc "aldeg-4-6_aldeg-4-7"; disablenpc "aldeg-5-7_aldeg-5-8";
		announce "[ WOE ] : สงครามแย่งชิงปราสาท Guild War of Emperium ได้เริ่มขึ้นแล้ว",0;
		initnpctimer;
		end;
	
	OnTimer5000:
		if($SAV_ONLINE)
			announce "[ WOE ] : * * * * วาร์ปชั้นที่ 2 จะเปิดในนาทีที่ 40 นะครับ * * * *",0;
		else
			announce "[ WOE ] : เนื่องจากยังไม่ถึงเวลาวอร์จริงวาร์ปชั้น 2 จะไม่ถูกเปิด",0;
		end;
		
	OnTimer2100000:
		if($SAV_ONLINE)
			announce "[ WOE ] : ========= อีก 5 นาทีจะเปิดวาร์ปเข้าปราสาทชั้นที่ 2 =========",0;
		end;
		
	OnTimer2220000:
		if($SAV_ONLINE)
			announce "[ WOE ] : ========= อีก 3 นาทีจะเปิดวาร์ปเข้าปราสาทชั้นที่ 2 =========",0;
		end;
	
	OnTimer2340000:
		if($SAV_ONLINE)
			announce "[ WOE ] : ========= อีก 1 นาทีจะเปิดวาร์ปเข้าปราสาทชั้นที่ 2 =========",0;
		end;
		
	OnTimer2395000:	
		if($SAV_ONLINE){
			for(set .@i, 5; .@i > 0; set .@i, .@i - 1){
				announce "[ WOE ] : ระบบจะเปิดวาร์ปของปราสาททำงานแล้ว " + .@i + " วินาที",0;
				sleep 1000;
			}
			announce "[ WOE ] : วาร์ปเข้าปราสาทชั้น 2 ถูกเปิดแล้ว",0;
			enablenpc "prtg-1-06_prtg-1-11"; enablenpc "prtg-2-05_prtg-2-11"; enablenpc "prtg-2-04_prtg-2-12"; enablenpc "prtg-3-05_prtg-3-13"; enablenpc "prtg-4-02_prtg-4-14"; enablenpc "prtg-4-06_prtg-4-13"; enablenpc "prtg-5-01_prtg-5-14";
			enablenpc "gefg-1-01_gefg-1-14"; enablenpc "gefg-2-01_gefg-2-12"; enablenpc "g3-cas3_g3-in3"; enablenpc "gefg-4-01_gefg-4-11"; enablenpc "gefg-5-06_gefg-5-11";
			enablenpc "payg-1-02_payg-1-11"; enablenpc "payg-2-05_payg-2-11"; enablenpc "payg-3-04_payg-3-13"; enablenpc "payg-4-03_payg-4-12"; enablenpc "payg-5-02_payg-5-14";
			enablenpc "aldeg-1-4_aldeg-1-10"; enablenpc "aldeg-2-5_aldeg-2-6"; enablenpc "aldeg-3-14_aldeg-3-5"; enablenpc "aldeg-4-6_aldeg-4-7"; enablenpc "aldeg-5-7_aldeg-5-8";
		}
		stopnpctimer;
		end;
	
}

// ป้องกันกระโดดขึ้นกำแพง (บัค cell)
-	script	gvg_cellnowalk	-1,{
	OnInit:
		setcell "gefg_cas03",42,233,42,233,cell_walkable,0;
		end;
}

// ======================= Duplicate Here =======================

moroccz,174,89,4	duplicate(WOE Information)	Warper GvG#1	4_F_SURA
moroccz,113,113,4	duplicate(WOE Information)	Warper GvG#2	4_F_SURA
moroccz,232,108,4	duplicate(WOE Information)	Warper GvG#3	4_F_SURA
moroccz,182,37,4	duplicate(WOE Information)	Warper GvG#4	4_F_SURA
moroccz,102,39,4	duplicate(WOE Information)	Warper GvG#5	4_F_SURA
moroccz,182,256,4	duplicate(WOE Information)	Warper GvG#6	4_F_SURA
moroccz,293,211,4	duplicate(WOE Information)	Warper GvG#7	4_F_SURA
moroccz,48,172,4	duplicate(WOE Information)	Warper GvG#8	4_F_SURA
moroccz,44,290,4	duplicate(WOE Information)	Warper GvG#9	4_F_SURA
moroccz,254,43,4	duplicate(WOE Information)	Warper GvG#10	4_F_SURA
moroccz,157,78,6	script	เควสแลกOCA#01	10185,{ 

callfunc "QuestShopOca"; 
end; 
OnInit: 
while (1) {
			showscript "[ เควสแลกสมุดการ์ด ]";
			sleep 500;
		}
end;
}

//============================================================
// Script Core
//============================================================

-	script	QuestShopOcaMenu	-1,{ 

function Add; 
function Chk; 
function Slot; 
function A_An;

OnInit:

	freeloop(1);

//============================================================
// Configulator Shop
//============================================================
																														   																							  
	set .Announce,1;	// ประกาศเมื่อได้เสร็จสินเควส ? (1: ใช่ / 0: ไม่)
	set .ShowSlot,1;	// แสดง สล๊อต ไอเท็ม ? (2: แสดงทั้งหมด / 1: ถ้า สล๊อตน้อยกว่า > 0 / 0: never)
	set .ShowID,1;  	// แสดง รหัสไอเท็ม ? (1: ใช่ / 0: ไม่)
	set .ShowZeny,1;	// แสดงผล Zeny ถ้ามีค่าใช้จ่าย, ? (1: ใช่ / 0: ไม่)
	set .MaxStack,1000;	// จำนวนสูงสุดของรายการเควส

//============================================================
// Points 
// setarray .Points$[0],"<ชื่อตัวแปร>","<การแสดงผล>";
//============================================================

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

//============================================================
// รหัสร้านค้า -- การเพิ่มร้านค้า, คัดลอกข้อมูลที่ด้านล่างของไฟล์.
// setarray .Shops$[1],"<เมนูร้านค้า 1>","<เมนูร้านค้า 2>"{,...};
//============================================================

	setarray .Shops$[1],"^FF0000^000000 เควสแลก OCA","^FF0000^000000 เควสแลก MCA","^FF0000^000000 เควสแลก Iara Card","^FF0000^000000 เควสแลก Piranha Card",

						
//===========================================================
// ร้านการ Item ที่ใช้ในการทำเควส
// Add(<รหัสร้านค้า>,<รหัสรางวัล>,<จำนวนรางวัล>,<ค่าใช้จ่าย Zeny>,<ค่าใช้จ่าย Cash Points>,<รหัสไอดี รายการที่ต้องหา>,<จำนวนของรายการที่ต้องหา>{,...});
//===========================================================
//===========================================================
//===========================================================

	Add(1,4001,1,0,0,616,1);
	Add(2,4004,1,0,0,12246,1);
	Add(3,4002,1,0,0,27120,1);
	Add(4,4003,1,0,0,27121,1);

//===========================================================
//===========================================================
//===========================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "QuestShopOca"+.@i,909;
	}
	end;

OnMenu:

	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"มีข้อผิดพลาดที่เกิดขึ้น.";
		end;
	}
	dispbottom "กรุณาเลือก รายการ ที่ท่านต้องการ.";
	callshop "QuestShopOca"+.@i,1;
	npcshopattach "QuestShopOca"+.@i;
	end;

OnBuyItem:

	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"คุณไม่สามารถซื้อ "+getitemname(.@q[0])+"ครั้งละ หลายรายการ.";
		end;
	}
	mes "[^FF0000เควสOCA^000000]";
	mes "รายการ ที่ี่เลือกคุณได้เลือก: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "รายการที่ คุณต้องมีเพื่อแลกเปลี่ยน:";
	disable_items;
	if (.@q[4]) mes " ^FF0000^000000 "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " ^FF0000^000000 "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " ^FF0000^000000 "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select("^FF0000^000000 แลกเปลี่ยน  ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?"^FF0000^000000 ทดลองใส่":"")+":^FF0000^000000 ^FF0000ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[^FF0000เควสOCA^000000]";
				mes "คุณไม่มี รายการจำเป็นที่ต้องใช้ในการแลกเปลี่ยน";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[^FF0000เควสOCA^000000]";
				mes "^FF0000น้ำหนักคุณเต็ม คุณจำเป็นต้องลดน้ำหนักลง "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" หน่วย เพื่อสำเร็จรายการนี้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			//if (.Announce) announce strcharinfo(0)+" ได้แลกเปลี่ยน "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+" สำเร็จ!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:

	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {

	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "QuestShopOca"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0); // หากต้องการให้แสดงราคาแบบ เซนี่ ให้ใช้อันนี้
//	npcshopadditem "QuestShopOca"+getarg(0),getarg(1),getarg(4); // หากต้องการให้แสดงราคาแบบ แคช ให้ใช้อันนี้

	return;
}

function Chk {

	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {

	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {

	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "-> "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	QuestShopOca	{

	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "QuestShopOcaMenu::OnMenu";
	end;
}


// ข้อมูลร้านจำลอง - คัดลอกตามต้องการ
//============================================================
-	shop	QuestShopOca1	-1,909:-1
-	shop	QuestShopOca2	-1,909:-1
-	shop	QuestShopOca3	-1,909:-1
-	shop	QuestShopOca4	-1,909:-1
-	shop	QuestShopOca5	-1,909:-1
-	shop	QuestShopOca6	-1,909:-1
-	shop	QuestShopOca7	-1,909:-1
-	shop	QuestShopOca8	-1,909:-1
-	shop	QuestShopOca9	-1,909:-1
-	shop	QuestShopOca10	-1,909:-1
-	shop	QuestShopOca11	-1,909:-1
-	shop	QuestShopOca12	-1,909:-1
-	shop	QuestShopOca13	-1,909:-1
-	shop	QuestShopOca14	-1,909:-1
-	shop	QuestShopOca15	-1,909:-1
-	shop	QuestShopOca16	-1,909:-1
-	shop	QuestShopOca17	-1,909:-1
-	shop	QuestShopOca18	-1,909:-1
-	shop	QuestShopOca19	-1,909:-1
-	shop	QuestShopOca20	-1,909:-1
-	shop	QuestShopOca21	-1,909:-1
-	shop	QuestShopOca22	-1,909:-1
-	shop	QuestShopOca23	-1,909:-1
-	shop	QuestShopOca24	-1,909:-1
-	shop	QuestShopOca25	-1,909:-1
-	shop	QuestShopOca26	-1,909:-1
-	shop	QuestShopOca27	-1,909:-1
-	shop	QuestShopOca28	-1,909:-1
-	shop	QuestShopOca29	-1,909:-1
-	shop	QuestShopOca30	-1,909:-1